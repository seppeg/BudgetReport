plugins {
    id 'java'
    id 'idea'
    id 'org.springframework.boot' version '2.0.2.RELEASE'
    id 'io.spring.dependency-management' version '1.0.4.RELEASE'
}

version '1.0-SNAPSHOT'

sourceCompatibility = 1.9
targetCompatibility = 1.9

repositories {
    mavenLocal()
    mavenCentral()
    maven { url "https://repo.spring.io/milestone" }
}

test {
    useJUnitPlatform {
        excludeTags 'slow'
        includeEngines 'junit-jupiter'
    }

    failFast = false
}

ext['hikaricp.version'] = '3.1.0'

ext{
    springCloudStreamVersion = '2.0.0.RELEASE'
    mockitoJUnitJupiterVersion = '2.18.3'
    guavaVersion = '25.0-jre'
}

dependencies {
    //spring
    compile 'org.springframework.boot:spring-boot'
    compile 'org.springframework.boot:spring-boot-autoconfigure'
    compile 'org.springframework:spring-core'
    compile 'org.springframework:spring-expression'

    //webflux
    compile 'org.springframework:spring-web'
    compile 'org.springframework:spring-webflux'
    compile 'io.projectreactor.ipc:reactor-netty'
    compile 'org.synchronoss.cloud:nio-multipart-parser'
    compile 'org.hibernate.validator:hibernate-validator'

    //actuator
    compile 'org.springframework.boot:spring-boot-actuator-autoconfigure'
    compile 'io.micrometer:micrometer-core'

    //cloud stream
    compile "org.springframework.cloud:spring-cloud-stream-binder-kafka:${springCloudStreamVersion}"
    compile "org.springframework.cloud:spring-cloud-stream:${springCloudStreamVersion}"
    compile "org.springframework.cloud:spring-cloud-starter-sleuth:${springCloudStreamVersion}"
    testCompile "org.springframework.cloud:spring-cloud-stream-test-support:${springCloudStreamVersion}"

    compile "net.logstash.logback:logstash-logback-encoder:5.1"

    //jackson
    compile 'com.fasterxml.jackson.core:jackson-databind'
    compile 'com.fasterxml.jackson.datatype:jackson-datatype-jdk8'
    compile 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'
    compile 'com.fasterxml.jackson.module:jackson-module-parameter-names'

    //persistence
    compile 'org.flywaydb:flyway-core'
    compile 'org.springframework.data:spring-data-jpa'
    compile 'org.springframework:spring-jdbc'
    compile 'org.hibernate:hibernate-core'
    compile 'com.zaxxer:HikariCP'
    compile 'org.postgresql:postgresql'
    testCompile 'com.h2database:h2'

    //aop
    compile 'org.springframework:spring-aspects'

    //jaxb
    compile 'javax.xml.bind:jaxb-api'

    compile "com.google.guava:guava:$guavaVersion"

    annotationProcessor 'org.projectlombok:lombok'
    compile 'org.projectlombok:lombok'

    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
    testCompile 'org.junit.jupiter:junit-jupiter-api'
    testCompile 'org.assertj:assertj-core'
    testCompile 'org.hamcrest:hamcrest-core'
    testCompile 'org.hamcrest:hamcrest-library'
    testCompile 'org.mockito:mockito-core'
    testCompile "org.mockito:mockito-junit-jupiter:$mockitoJUnitJupiterVersion"

}
